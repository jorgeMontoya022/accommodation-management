plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'io.qameta.allure' version '2.12.0'
}

group = 'co.edu.uniquindio.alojamientos'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.simplejavamail:simple-java-mail:8.12.6'
    implementation 'org.simplejavamail:batch-module:8.12.6'

    // Cloudinary
    implementation 'com.cloudinary:cloudinary-http5:2.3.2'

    // Seguridad y JWT
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // *Binding Lombok–MapStruct (clave)*
    implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // OpenAPI UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

    // mappers en tests
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
}

//CONFIGURACIÓN DEL COMPILADOR JAVA
tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.unmappedTargetPolicy=WARN'
    ]
}

//CONFIGURACIÓN DE TESTS
test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

//  JACOCO (COBERTURA DE CÓDIGO)
jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }

    def excludes = [
            '**/config/**',
            '**/Application.class',
            '**/dto/**',
            '**/generated/**',
            '**/mapper/**Impl.class'  // Excluir implementaciones generadas de MapStruct
    ]

    classDirectories.setFrom(
            files(sourceSets.main.output).collect {
                fileTree(dir: it, exclude: excludes)
            }
    )

    additionalSourceDirs.setFrom(files(sourceSets.main.allSource.srcDirs))
    sourceDirectories.setFrom(files(sourceSets.main.allSource.srcDirs))
    executionData.setFrom(fileTree(dir: project.buildDir, include: '/jacoco/*.exec'))
}

jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

// ALLURE
allure {
    version = '2.24.0'
}